// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/RAM8.hdl
/**
 * Memory of eight 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    DMux8Way(
        in=load, 
        sel=address, 
        a=r1Load, b=r2Load, c=r3Load, d=r4Load, 
        e=r5Load, f=r6Load, g=r7Load, h=r8Load 
    );
    
    Register(in=in, load=r1Load, out=r1);
    Register(in=in, load=r2Load, out=r2);
    Register(in=in, load=r3Load, out=r3);
    Register(in=in, load=r4Load, out=r4);
    Register(in=in, load=r5Load, out=r5);
    Register(in=in, load=r6Load, out=r6);
    Register(in=in, load=r7Load, out=r7);
    Register(in=in, load=r8Load, out=r8);
    
    Mux8Way16(
        a=r1, b=r2, c=r3, d=r4, 
        e=r5, f=r6, g=r7, h=r8, 
        sel=address, 
        out=out
    );
}